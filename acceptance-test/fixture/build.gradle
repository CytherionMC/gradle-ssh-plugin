buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        if (gradle.gradleVersion.startsWith('1.')) {
            classpath 'org.codehaus.groovy:groovy-backports-compat23:2.3.6'
        }
        classpath "org.hidetake:gradle-ssh-plugin:${property('target.product.version')}"
    }
}

apply plugin: 'org.hidetake.ssh'

remotes {
    testServer {
        role 'testServers'
        host = 'sandbox.local'
        port = 8022
        user = 'tester'
        identity = file("${System.getProperty('user.home')}/.ssh/id_ext")
        knownHosts = file("${System.getProperty('user.home')}/.ssh/known_hosts_ext")
    }
}

ext.localWorkDirBase = buildDir
ext.remoteWorkDirBase = '/tmp/gradle-ssh-plugin.acceptance-test'

task setup << {
    println "Gradle: ${gradle.gradleVersion}"
    println "Java: ${System.getProperty('java.home')}"
    println "Target: ${ssh.version}"
    delete buildDir
    buildDir.mkdirs()
    ssh.run {
        session(remotes.testServer) {
            remove remoteWorkDirBase
            execute "mkdir -vp $remoteWorkDirBase"
        }
    }
}

task cleanup << {
    delete buildDir
    ssh.run {
        session(remotes.testServer) {
            remove remoteWorkDirBase
        }
    }
}
