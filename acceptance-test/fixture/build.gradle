buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        if (gradle.gradleVersion.startsWith('1.')) {
            classpath 'org.codehaus.groovy:groovy-backports-compat23:2.3.6'
        }
        classpath "org.hidetake:gradle-ssh-plugin:${property('target.product.version')}"
    }
}

apply plugin: 'org.hidetake.ssh'

remotes {
    testServer {
        role 'testServers'
        host = 'sandbox.127.0.0.1.xip.io'
        port = 8022
        user = 'tester'
        identity = file("$buildDir/id_rsa")
        knownHosts = file("$buildDir/known_hosts")
    }
}

ext.localWorkDirBase = file("$buildDir/tmp")
ext.remoteWorkDirBase = '/tmp/gradle-ssh-plugin.acceptance-test'

task setup << {
    println "Gradle: ${gradle.gradleVersion}"
    println "Java: ${System.getProperty('java.home')}"
    println "Target: ${ssh.version}"
    delete localWorkDirBase
    localWorkDirBase.mkdirs()
    ssh.run {
        session(remotes.testServer) {
            remove remoteWorkDirBase
            execute "mkdir -vp $remoteWorkDirBase"
        }
    }
}

task cleanup << {
    delete localWorkDirBase
    ssh.run {
        session(remotes.testServer) {
            remove remoteWorkDirBase
        }
    }
}
