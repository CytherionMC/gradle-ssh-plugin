plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['--release', '8'])
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile('org.hidetake:groovy-ssh:2.10.0') {
        exclude module: 'groovy-all'
    }
    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.10'
}

group = 'org.hidetake'
version = System.getenv('CIRCLE_TAG') ?: 'SNAPSHOT'

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

pluginBundle {
    website = 'https://github.com/int128/gradle-ssh-plugin'
    vcsUrl = 'https://github.com/int128/gradle-ssh-plugin'
    description = 'Gradle SSH Plugin'
    tags = ['ssh']

    plugins {
        swaggerPlugin {
            id = 'org.hidetake.ssh'
            displayName = 'Gradle SSH Plugin'
        }
    }
}
