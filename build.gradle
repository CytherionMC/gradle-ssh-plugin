plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'ivy-publish'
    id 'com.jfrog.bintray' version '0.6'
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile('org.hidetake:groovy-ssh:0.1.2') {
        exclude module: 'groovy-all'
    }
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.apache.sshd:sshd-core:0.9.0'
}

test {
    useJUnit {
        excludeCategories 'org.hidetake.gradle.ssh.server.ServerIntegrationTest'
    }
}

task serverIntegrationTest(type: Test) {
    useJUnit {
        includeCategories 'org.hidetake.gradle.ssh.server.ServerIntegrationTest'
    }
    reports {
        html.destination = file("$buildDir/reports/server-integration-tests")
    }
}

jacoco {
    toolVersion = '0.7.1.201405082137'
}

task jacocoServerIntegrationTestReport(type: JacocoReport) {
    executionData serverIntegrationTest
    sourceSets sourceSets.main
    reports.html.destination "$buildDir/reports/jacoco/server-integration-tests"
}

check.dependsOn serverIntegrationTest
check.dependsOn jacocoTestReport
check.dependsOn jacocoServerIntegrationTestReport

if (System.getProperty('os.name') == 'Linux') {
    tasks.withType(JavaForkOptions) {
        systemProperty 'java.security.egd', 'file:/dev/./urandom'
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url project.property('pom.url')
                    scm {
                        url project.property('pom.scm.url')
                        connection project.property('pom.scm.connection')
                    }
                    licenses {
                        license {
                            name project.property('pom.license.name')
                            url project.property('pom.license.url')
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id project.property('pom.developer.id')
                            name project.property('pom.developer.name')
                        }
                    }
                }
            }
        }
        ivy(IvyPublication) {
            from components.java
        }
    }
    repositories {
        ivy {
            url ivyRepository
        }
    }
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayKey
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        version {
            attributes = ['gradle-plugin': 'org.hidetake.ssh:org.hidetake:gradle-ssh-plugin']
        }
    }
}

tasks.bintrayUpload.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

task install(dependsOn: 'publishIvyPublicationToIvyRepository') {
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task archiveDependencies(type: Zip, dependsOn: jar) {
    classifier = 'all'
    into('dep-libs') {
        from configurations.runtime
        from 'build/libs'
    }
}
